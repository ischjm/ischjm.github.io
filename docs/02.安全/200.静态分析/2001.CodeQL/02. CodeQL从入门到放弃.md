---
title: CodeQL从入门到放弃
date: 2022-04-05 00:00:00
permalink: /sec/sa/codeql/codeqlstep0to1
author:
 name: p0jo
 link: https://wiki.pwddd.com
categories:
  - 静态分析
  - CodeQL
tags:
  - CodeQL
---

# CodeQL从入门到放弃

> 这篇文章是我在学习CodeQL时的笔记，该文章完全参考了l4yn3师傅在FreeBuf上发表《CodeQL从入门到放弃》。文章的链接会放到参考资料中。至于为什么要重新写一篇类似的文章，只是单纯为了记录一下自己学习CodeQL使用的过程遇到的问题。

<!-- more -->

## 为什么是CodeQL

CodeQL作为代码安全审计的一款十分好用和高效的工具，并不是凭空出现的。还要从代码审计的历史说起。

在最早期的时候，由于系统规模较小，代码量还比极少，代码之间的逻辑调用关系也很清晰，安全人员通常采用人工的方式去寻找代码中存在的安全问题。而人工审计一般有三种常见的审计思路：第一种是全文通读，在全面的阅读代码之后，对代码有一个全面的理解，知道每一部分代码的功能，然后结合具体的功能发现代码中存在的问题，这种审计的方式十分的细致，但是也十分的耗时；第二种方式是定位危险的函数，然后向上追溯到危险数据输入的位置，如果数据流没有中断则存在一条从用户输入到危险函数的通路，这种方式虽然快捷，但是不全面，对应一些逻辑漏洞无能为力；第三种方式是针对一些特定的功能点进行威胁的建模和审计，这种方式相对较快且对于逻辑漏洞也能有很好的把握。

但是随着项目复杂度和代码量上的指数级增长，人工的方式很难进行全面的审计，因此就需要一些软件的介入，配合人工完成代码审计。这一阶段的一些工具例如rips、Cobra可以帮助定位到危险函数，然后再由人工确认是否存在漏洞。这种方式也是需要依靠人工进行判定，安全人员的压力还是比较大，因此也就迭代出了一些自动化的代码审计产品，例如常用的Checkmarx、Fortify SCA等。CodeQL也是属于这一类型的工具，相较于其他几种工具而言，CodeQL开源、可自定义程度高的优点十分值得安全人员学习和使用。



## 环境介绍

这里使用的是l4yn3师傅搭建的靶场，该靶场是使用SpringBoot搭建的，里面包含了一些常见漏洞类型。这一篇就利用CodeQL去自动检索这些漏洞，并减少误报和漏报问题。我的环境配置如下：

- 操作系统：Mac OS
- Java版本：1.8.0_311
- maven版本：3.8.4
- 靶场下载地址：https://github.com/l4yn3/micro_service_seclab/



## CodeQL创建并导入数据库

对于CodeQL的环境搭建包括VSCode、CodeQL CLI的相关配置在之前的文章中有详细写过，这里就不再赘述。

在配置完成环境并且将靶场代码克隆到本地之后，我们使用CodeQL CLI的相关命令生成一个数据库：

```shell
codeql database create ./seclab_db --language=java --command="mvn clean install --file pom.xml" --source-root=./micro_service_seclab

# --language 指定语言
# --command="mvn clean install --file pom.xml" 编译命令Java语言使用maven进行编译
# --source-root=  项目路径
```

最终出现Successfully created database代表数据库创建完成了，在创建过程中如果发生报错，可以删除数据库再次运行。注意要保持全路径中没有空格存在。

![image-20220405074746592](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405074746592.png)

在创建完成数据库之后，可以将数据库导入到VSCode中。

![image-20220405075353853](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405075353853.png)

运行hello world测试环境是否正常。

```ql
select "helloworld"
```

![image-20220405075505400](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405075505400.png)

至此，环境已经搭建好了，数据库也已经导入了。后面就开始编写相应的查询语句。

## CodeQL 基础语法

### 基础QL

我们利用CodeQL的命令创建了CodeQL数据库，然后需要使用CodeQL提供的开源的规则库编写相关的规则，发现安全漏洞。

![img](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/1628394186_610f52ca969589d7b9214.png)

CodeQL是一种类SQL的查询语句，主要由四部分组成：

```ql
import java # 导入语句 导入相关的库

from ... # 定义相关的变量
where # 设置查询的条件
select # 显示查询的结果
```

在from语句中，我们通常使用CodeQL提供给我们的很多类库来声明变量。我们经常使用的类库有如下的几个：

|     名称     |                             解释                             |
| :----------: | :----------------------------------------------------------: |
|    Method    |      方法类，Method method表示获取当前项目中所有的方法       |
| MethodAccess | 方法调用类，MethodAccess call表示获取当前项目当中的所有方法调用 |
|  Parameter   |       参数类，Parameter表示获取当前项目当中所有的参数        |

比如我们可以使用查询语句找出所有名为`get*`的方法。

```ql
import java

from Method method
where method.getName().regexpMatch("get.*")
select method.getName(),method.getDeclaringType()
```

我们可以将where后面的查询条件封装成一个函数，函数的参数为method对象，函数中判断该method是否符合条件，返回True或者false。

```ql
import java

predicate isGetMethod(Method method) {
    exists( | method.getName().regexpMatch("get.*"))
}

from Method method

where isGetMethod(method)
select method.getName(),method.getDeclaringType()
```



### 设置source和sink

sources、sinks、sanitizer是污点分析中的核心三元组。sources指的是漏洞污染的输入点；sinks指的是漏洞的执行位置；sanitizer称为净化函数，阻断了从sources到sinks的通路。只有当sources和sinks同时存在，并且不存在一个sanitizer阻断sources到sinks的通路则漏洞是存在的。

![img](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/1628394339_610f53637de77e9114e4f.jpg)

在web项目中，source是用户的输入。如果使用的是spring框架搭建的项目，source可以是`RequestMapping`。我们不需要自己去编写source的规则，CodeQL官方提供了Source，其中包含了大部分的Source入口，常见的Spring框架的入口也在里面。

```ql
override predicate isSource(DataFlow::Node src) {
    src instanceof RemoteFlowSource
}	
```

在这个靶场中，使用的JDBCTemplates，因此在使用query方法进行查询时，传入sql语句，就是本例中的sinks。sinks的设置使用到了exists子查询语法，exists的语法是：`exists(声明变量|判断表达式)`。因此我们的sink设置如下：

```ql
override predicate isSink(DataFlow::Node sink) {
    exists(Method method,MethodAccess call | 
        method.hasName("query")
        and call.getMethod() = method
        and sink.asExpr() = call.getArgument(0)
        )
}
```

### 判断连通

我们设置了source和sink，后面比较关键的一步就是判断从source到sink是否连通。因为如果漏洞存在，则必须要从source到sink存在一条通路。好在这项工作由CodeQL完成，CodeQL使用`hasFlowPath`判断是否连通，参数只需要传入我们设置好的source和sink。

```ql
from VulConfig config , DataFlow::PathNode source , DataFlow::PathNode sink
where config.hasFlowPath(source, sink)
select source.getNode(),source,sink,"source"
```

### 小结版本

到目前为止，我们设置了source、sink，并且使用CodeQL的`hasFlowPath`判断连通，下一步需要将这些东西组装起来。我们在前面有提到过CodeQL class的概念，我们这里也只需要继承CodeQL提供的用于数据流分析的父类`TaintTracking::Configuration`即可。最终我们第一版本的代码如下：

```ql
import java
import semmle.code.java.dataflow.FlowSources
import semmle.code.java.security.QueryInjection
import DataFlow::PathGraph

class VulConfig extends TaintTracking::Configuration {
    VulConfig(){
        this = "sqlInjectionConfig"
    }

    override predicate isSource(DataFlow::Node src) {
        src instanceof RemoteFlowSource
    }

    override predicate isSink(DataFlow::Node sink) {
        exists(Method method,MethodAccess call | 
            method.hasName("query")
            and call.getMethod() = method
            and sink.asExpr() = call.getArgument(0)
            )
    }

}

from VulConfig config , DataFlow::PathNode source , DataFlow::PathNode sink

where config.hasFlowPath(source, sink)
select source.getNode(),source,sink,"source"

```

另外需要注意，注释部分十分的重要，包含了查询的源信息，和最终结果的展示也有很大的关系，因此必须要带上。

```ql
/**
 * @id java/examples/vuldemo
 * @name Sql-Injection
 * @description Sql-Injection
 * @kind path-problem
 * @problem.severity warning
 */
```

![image-20220405202830038](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405202830038.png)

## 误报处理

上一步中得出的一些结果中，有一个结果是不可能产生SQL注入问题的，属于误报。当参数类long类型时，恶意SQL语句无法进行拼接和传参，因此不存在SQL注入的可能，我们需要将其从结果中排除。

![image-20220405203437413](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405203437413.png)

我们排出误报的方法就是使用sanitizer

![img](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/1628394471_610f53e70361d448d1bbe.jpg)

CodeQL提供了一个`isSanitizer`方法，该方法和`isSource`、`isSink`一样，都是`TaintTracking::Configuration`提供的净化方法。CodeQL在该方法中提供了默认的基础数据类型的过滤，但是对于List这种复合的数据类型需要我们加入到`isSanitizer`中。

```ql
override predicate isSanitizer(DataFlow::Node node){
    node.getType() instanceof PrimitiveType or
    node.getType() instanceof BoxedType or
    node.getType() instanceof NumberType or
    exists(ParameterizedType pt | node.getType() = pt 
        and pt.getTypeArgument(0) instanceof NumberType )
}
```

以上代码的意思为：如果当前node节点的类型为基础类型，数字类型和泛型数字类型(比如List)时，就切断数据流，认为数据流断掉了，不会继续往下检测。重新执行query，误报就已经解决了。



## 漏报解决

根据ly4n3师傅的文章，存在一个漏报，因为Optional这种类型的使用没有在CodeQL的语法库里，所以`username.get()`无法识别就断掉了。但是我在测试的时候发现，似乎CodeQL能够识别这条通路了，可能CodeQL更新了吧。这里就简单提一下如果出现了漏报怎么解决。

CodeQL提供了一个`isAdditionalTaintStep`方法，该方法可以将断掉的数据通路接上。

![img](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/1628394505_610f5409ded2fb0984439.jpg)

`isAdditionalTaintStep`方法是CodeQL的类`TaintTracking::Configuration`提供的的方法。它的作用是将一个可控节点，A强制传递给另外一个节点B，那么节点B也就成了可控节点。

l4yn3给的代码：

```ql
/**
 * @id java/examples/vuldemo
 * @name Sql-Injection
 * @description Sql-Injection
 * @kind path-problem
 * @problem.severity warning
 */

import java
import semmle.code.java.dataflow.FlowSources
import semmle.code.java.security.QueryInjection
import DataFlow::PathGraph

predicate isTaintedString(Expr expSrc, Expr expDest) {
    exists(Method method, MethodAccess call, MethodAccess call1 | expSrc = call1.getArgument(0) and expDest=call and call.getMethod() = method and method.hasName("get") and method.getDeclaringType().toString() = "Optional<String>" and call1.getArgument(0).getType().toString() = "Optional<String>"  )
}

class VulConfig extends TaintTracking::Configuration {
  VulConfig() { this = "SqlInjectionConfig" }

  override predicate isSource(DataFlow::Node src) { src instanceof RemoteFlowSource }

  override predicate isSanitizer(DataFlow::Node node) {
    node.getType() instanceof PrimitiveType or
    node.getType() instanceof BoxedType or
    node.getType() instanceof NumberType or
    exists(ParameterizedType pt| node.getType() = pt and pt.getTypeArgument(0) instanceof NumberType )
  }

  override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
      method.hasName("query")
      and
      call.getMethod() = method and
      sink.asExpr() = call.getArgument(0)
    )
  }
override predicate isAdditionalTaintStep(DataFlow::Node node1, DataFlow::Node node2) {
    isTaintedString(node1.asExpr(), node2.asExpr())
  }
}


from VulConfig config, DataFlow::PathNode source, DataFlow::PathNode sink
where config.hasFlowPath(source, sink)
select source.getNode(), source, sink, "source"
```



## 参考资料

l4yn3 Freebuf：https://www.freebuf.com/articles/web/283795.html

CodeQL 文档：https://codeql.github.com/docs/