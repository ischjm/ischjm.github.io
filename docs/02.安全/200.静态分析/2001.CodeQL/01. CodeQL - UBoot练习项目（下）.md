---
title: CodeQL - UBoot练习项目（下）
date: 2022-04-04 00:00:00
permalink: /sec/sa/codeql/uboot2
author:
 name: p0jo
 link: https://wiki.pwddd.com
categories:
  - 静态分析
  - CodeQL
tags:
  - CodeQL
---

# CodeQL - UBoot练习项目（下）


> 在上一篇《CodeQL - Uboot练习（上）》介绍了Gitlab提供的一个用于熟悉CodeQL用法的练手项目CodeQL-uboot，同时也按照项目的要求搭建了CodeQL的基础使用环境。在这一篇中，就开始根据项目的要求，完成查询语句的编写。

<!-- more -->

##  3. 第一个查询

上一篇中，我们克隆了uboot项目代码，并且将其添加到了vscode-starter中。这一篇中的所有代码都在这个项目中进行编写和提交。第三节的目的是完成一个最简单的查询语句的编写，并提交到github仓库中，由机器人判断你的代码是否正确。

编辑`3_function_definitions.ql`，输入以下内容：

```ql
import cpp

from Function f
where f.getName() = "strlen"
select f, "a function named strlen"
```

最好是自己输入这些内容，而不是直接复制粘贴。在输入的过程中，可以看到vscode的自动提示。

右击，点击`CodeQL: Run Query`，你会发现程序在右侧展示了运行的结果。

![image-20220404221714270](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220404221714270.png)

这个查询可以找出程序中的所有名为`strlen`方法，并将其显示到结果中，我们点击结果中的方法名，可以定位到代码的位置。



以上内容就是第三节的查询内容了，第三节的后面部分提供了两种代码提交的方式，代码提交在后续的每一节都会用得到，如果不熟悉的话，建议多看看。

### 通过 Pull Request提交你的请求

通过PR提交往往应用在多人共同维护一个代码仓库时。通过PR的方式提交可以直接在PR中跟踪检查器的执行情况。

但是这种方式要比直接提交到main分支上更复杂。提交的流程如下：

1. 刷新主分支的内容，将代码提交到该分支，然后推送

   ```shell
   git checkout main
   git pull
   git checkout -b step-3
   git add .
   git commit -a -m "First Query"
   git push -u origin step-3
   ```

2. 发起一个PR

3. 等待检查完毕，并在PR的过程中显示进度。

4. 检查完成之后，如果显示绿色则代表通过。

### 提交到main分支

这种方式比PR的方式简单很多，我们在后续的提交中都会使用这种方式：

1. 将代码推送到课程的远程仓库中

   ```shell
   git add .
   git commit -m "Any message here - why not step 3"
   git push origin main
   ```

2.等待检查完成，检查完成后进入下一节。检查失败则可以编辑我们的查询语句，再次提交。

## 4. 解析查询

### 解析

这一节中，对上一节我们编写的查询语句做了一个分析，并且让我们简单改写一下上面的查询完成相应的查询任务。

CodeQL中的查询一般都有如下的结构：

```ql
import 引入一个CodeQL库

from 变量的声明部分
where 查询的逻辑
select 输出的结果
```

再回顾一下我们上一课的查询语句：

```ql
import cpp

from Function f
where f.getName() = "strlen"
select f, "a function named strlen"
```

第一句是`import cpp`，这是一个导入的语句，后续我们在查询中使用的所有的声明等，都是通过这个语句导入的。

第二句是`from Function f`，声明了一个变量f，其中Function是一个类，该类是C/C++中的所有的函数的集合。

在where后面接着的是我们查询的谓词，谓词这个概念在后面也常被提到，可以简单理解为一个函数，像这里面的`getName`就是一个谓词，这个为此有一个返回值，当这个返回值满足等于`strlen`条件时，就按照selct的要求显示结果。



### 找到名为memcpy的函数

1. 编辑`4_memcpy_definitions.ql`
2. 按照第三部的查询中的查询语句，将查询的条件修改为查询名字为`memcpy`。
3. 运行查询语句，查看查询的结果
4. 提交你的查询到远程仓库

```ql
import cpp

from Function f 
where f.getName() = "memcpy"
select f, "function named memcpy"
```



## 5. 使用不同的类和谓词

在第四节中，我们使用了Function类并使用了`getName`获取了所有的方法名。而在这一节中，我们需要查询给出的三个转换字节序的宏（宏是C语言中的一个重要的定义，不熟悉C的朋友也不用担心，这里只是用来引入其他的一些查询的类）。这次的查询代码和上次的类似：

```ql
import cpp

from Macro m
where m.getName() = "ntohl" 
        or m.getName() = "ntohll"
        or m.getName() = "ntohs"
select m,"macro named ntoh*"
```

由于查询的条件有三个，`ntohl、ntohll、ntohs`，我们可以使用or来并列多个查询条件。也可以使用正则表达式的写法：

```ql
where m.getName().regexpMatch("ntoh(s|l|ll)")
```

或者使用in的方式：

```ql
where m.getName() in ["ntohs","ntohl","ntohll"]
```

以上三种方式均可。



## 6. 使用并关联两个变量

以上的几节中，在from后面我们只声明了一个变量，在这一节我们需要声明两个变量，并使用两个变量之间的关系联合完成相关的查询。这种方式有助于帮我们找出函数的调用。

1. 编辑`6_memcpy_calls.ql`
2. 使用代表函数调用的类并声明变量
3. 使用函数调用类的相关谓词查找函数的调用位置
4. 和第四步中的函数查找结合，找出函数memcpy的调用位置
5. 完成并运行查询，没有问题后，提交查询

```ql
import cpp

from Function f, FunctionCall call

where call.getTarget() = f 
    and f.getName() = "memcpy"
select call
```



## 7. 继续关联两个变量

在第五节中，编写了一个查询，查找所有的名为`ntoh*`的宏。在第六节中编写了一个查询，查找所有名为`memcpy`的函数的调用位置。而在这一节中，需要找到以上名为`ntoh*`的宏的调用位置。

1. 编辑`7_macro_invocations.ql`
2. 使用自动补全，找出查找宏调用的类
3. 找出代表宏调用的谓词
4. 查询宏的名称和宏的调用位置
5. 运行查询，提交代码

```ql
import cpp

from Macro m, MacroInvocation mi
where m.getName().regexpMatch("ntoh(s|l|ll)")
    and mi.getMacro() = m
select m,mi
```



## 8. 更改输出

在上一节我们找到了所有名为`ntoh*`的定义，并且查找了所有的调用位置，而在这一节中，通过 `MacroInvocation` 这个类来查询 "ntohs" 等宏的调用，并通过 `getExpr()` 这个方法进行宏的展开，得到相应的代码片段。

```ql
import cpp

from MacroInvocation mi
where mi.getMacroName().regexpMatch("ntoh(s|l|ll)")
select mi.getExpr()
```



## 9. 编写自己的类

在上面的查询中，我们使用过`Function、FunctionCall、Marco、MarcoInvocation`等类，这些类都是CodeQL提供给我们调用的。我们也可以定义一个自己的类，在本节中，通过实现一个自己的类，能自定义一个自己想要的数据集合。如果有点懵可以看看 CodeQL 关于类的[相关文档](https://link.zhihu.com/?target=https%3A//help.semmle.com/QL/ql-handbook/types.html%23classes)。查询语句中的类中，先通过 `exists` 量词创建一个临时变量 `mi` 来表示被调用的宏的名字，如果被调用的的宏展开后和当前代码片段相等，则这个表达式属于这个集合。

在这一节中，我们编写的类将上一节的内容进行一个封装，封装完成之后，只需要调用相应的类即可查询出结果。

1. 编辑`9_class_network_byteswap.ql`
2. 编写一个类，继承Expr，Expr是所有表达式的类
3. 使用exists，完成我们需要的查询，并返回相应的结果
4. 修改如下的模版

```ql
import cpp

class NetworkByteSwap extends Expr {
  NetworkByteSwap () {
    // TODO: replace <class> and <var>
    exists(<class> <var> |
      // TODO: <condition>
    )
  }
}

from NetworkByteSwap n
select n, "Network byte swap"
```

	5. 完成查询，运行并提交

```ql
import cpp

class NetworkByteSwap extends Expr {
  NetworkByteSwap() {
    exists(MacroInvocation mi |
      mi.getMacroName().regexpMatch("ntoh(s|l|ll)") and
      this = mi.getExpr()
    )
  }
}

from NetworkByteSwap n
select n, "Network byte swap"
```



## 10. 数据流和污点追踪

第十节是这个项目的最后一节。在第九步，我们在代码中发现了一些可以远程输入的表达式。他们可以被认为是远程输入的 **源**。源这个概念是污点分析的重要概念，简单理解就是用户输入恶意代码的位置。在第六步中，我们发现了memcpy的调用，这些调用可能是不安全的，因为他们的长度是被远程用户可以控制的。这些可以称为汇聚点，汇聚点是产生不安全操作的位置。这里的memcpy接受可控制的长度的值。

通过组合上面的信息，我们知道当数据流从一个用户可控的source流向到存在风险的sink位置的时候，一个程序是存在安全风险的。但是我们是怎么知道数据从一个source流向sink的呢？这里提到了**数据流、污点分析技术。**因为可能存在不同的source和sink，这样组合起来，如果使用人工的方式，可能产生大量的工作量。CodeQL替我们进行分析。你只需要编写一个查询，就可以发现9个真实存在的安全风险。

为了达到这个目标，我们使用CodeQL污点分析库，这个库可以定义source和sink，当对应的数据从source流转到sink时，hashFlowPath成立。

在这一节中，我们需要编写一个污点分析的查询。

1. 编辑`10_taint_tracking.ql`

2. 使用如下的模板文件

   ```ql
   /**
    * @kind path-problem
    */
   
   import cpp
   import semmle.code.cpp.dataflow.TaintTracking
   import DataFlow::PathGraph
   
   class NetworkByteSwap extends Expr {
     // TODO: copy from previous step
   }
   
   class Config extends TaintTracking::Configuration {
     Config() { this = "NetworkToMemFuncLength" }
   
     override predicate isSource(DataFlow::Node source) {
       // TODO
     }
     override predicate isSink(DataFlow::Node sink) {
       // TODO
     }
   }
   
   from Config cfg, DataFlow::PathNode source, DataFlow::PathNode sink
   where cfg.hasFlowPath(source, sink)
   select sink, source, sink, "Network byte swap flows to memcpy"
   ```

3. 复制第九步的自定义类，编写is Source和isSink定义源和汇聚。

   - 在第九步已经编写了识别`ntoh*`的类，这里只需要使用instance关键字进行判断即可
   - 注意`source`变量是类型的`DataFlow::Node`，而你的`NetworkByteSwap`类是的子类`Expr`，所以我们不能只写`source instanceof NetworkByteSwap`。

4. 运行查询，提交。

```ql

/**
 * @kind path-problem
 */

import cpp
import semmle.code.cpp.dataflow.TaintTracking
import DataFlow::PathGraph

class NetworkByteSwap extends Expr {
  NetworkByteSwap() {
    exists(MacroInvocation mi |
      mi.getMacroName().regexpMatch("ntoh(s|l|ll)") and
      this = mi.getExpr()
    )
  }
}

class Config extends TaintTracking::Configuration {
  Config() { 
      this = "NetworkToMemFuncLength" 
    }

  override predicate isSource(DataFlow::Node source) { 
      source.asExpr() instanceof NetworkByteSwap 
    }
  override predicate isSink(DataFlow::Node sink) {
    exists(FunctionCall call |
      call.getTarget().getName() = "memcpy" and
      sink.asExpr() = call.getArgument(2) and
      not call.getArgument(1).isConstant()
    )
  }
}

from Config cfg, DataFlow::PathNode source, DataFlow::PathNode sink
where cfg.hasFlowPath(source, sink)
select sink, source, sink, "Network byte swap flows to memcpy"
```

新定义的 `Config` 类继承于 `TaintTracking::Configuration`。在这个类中，对 `isSource` 和`isSink`进程重载，`isSource`谓语定义为污点的源头，而 `isSink` 定义为污点的去处。

有时候，远程输入的数据可能经过 `ntoh` 函数处理，通过转换字节序得到相应的数字。而 `memcpy` 的第 2 个参数如果控制不当，可造成数据溢出。将上面两个结论结合起来，如果有一个远程输入的数据通过字节序变换得到的数字，在未经过校验的情况下，作为了 `memcpy` 的第二个参数，那么就有可能造成数据溢出。

照着 [Github 安全团队的文章](https://link.zhihu.com/?target=https%3A//securitylab.github.com/research/cve-2018-4259-macos-nfs-vulnerability)，可以照猫画虎的补全数据查询语句。

在 `isSource` 中，我们通过判断 `source` 的 `Expr` 是否是 `NetworkByteSwap` 这个类，来判断污点的源头。

在 `isSink` 中，我们使用了辅助类 `FunctionCall` 判断函数调用是否为 `memcpy` 且 `sink` 的代码片段是否为 `memcpy` 的第二个参数；最后一句则是判断函数的第一个参数是否为常量，如果为常量的话基本不可能出现问题，所有忽略。

![image-20220405002009580](https://wiki-1251603812.cos.ap-shanghai.myqcloud.com/images/image-20220405002009580.png)



## 参考资料

1. https://zhuanlan.zhihu.com/p/137569940
2. https://codeql.github.com/docs/ql-language-reference